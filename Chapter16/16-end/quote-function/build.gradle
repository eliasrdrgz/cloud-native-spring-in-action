plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.graalvm.buildtools.native' version '0.10.2'
	id 'org.cyclonedx.bom' version '1.8.2'
    id("org.openrewrite.rewrite") version("6.16.0")
}

rewrite {
    activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_3")
	activeRecipe("org.openrewrite.java.migrate.UpgradeToJava21")
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

description = 'Provides quotes from the books in the library.'

repositories {
	maven { url 'https://repo.spring.io/milestone' }
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.9")
}

dependencies {
    rewrite("org.openrewrite.recipe:rewrite-spring:5.12.0")
	
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-function-web'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootBuildImage') {
	builder = 'docker.io/paketobuildpacks/builder-jammy-tiny'
	environment = ['BP_NATIVE_IMAGE': 'true']
	imageName = "${project.name}"

	docker {
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}
